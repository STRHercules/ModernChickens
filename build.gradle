import org.gradle.api.file.RelativePath
import java.util.Locale

plugins {
    id 'net.neoforged.moddev' version '1.0.11'
    id 'maven-publish'
}

version = '1.0.6'
group = 'com.setycz.chickens'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'build/generated/resources'
            srcDir layout.buildDirectory.dir('generated/roostAssets')
        }
    }
}

def roostTextureSourceDir = layout.projectDirectory.dir('roost/src/main/resources/assets/roost/textures/blocks')
def generatedRoostDir = layout.buildDirectory.dir('generated/roostAssets')

tasks.register('generateRoostTextures', Copy) {
    onlyIf { roostTextureSourceDir.asFile.exists() }
    from(roostTextureSourceDir) {
        include 'curtain_empty.png', 'curtain_side.png', 'hay_floor.png', 'hay_side.png', 'plain_face.png', 'slat_side.png'
        include 'chicken/*.png'
        into 'assets/chickens/textures/block'
    }
    into generatedRoostDir
    includeEmptyDirs = false
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = 'https://maven.blamejared.com' }
}

tasks.named('processResources') {
    notCompatibleWithConfigurationCache("Resource remapping mutates RelativePath instances at execution time")
    dependsOn 'generateRoostTextures'
    from('OriginalChickens/src/main/resources')
    from(generatedRoostDir)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    inputs.property('modVersion', version)
    filesMatching('META-INF/neoforge.mods.toml') {
        expand('file': ['jarVersion': project.version])
    }
    eachFile { details ->
        def segments = details.relativePath.segments.clone()
        if (segments.length >= 4) {
            if (segments[3] == 'blocks') {
                segments[3] = 'block'
                details.relativePath = new RelativePath(details.relativePath.isFile(), segments)
            } else if (segments[3] == 'items') {
                segments[3] = 'item'
                details.relativePath = new RelativePath(details.relativePath.isFile(), segments)
            }
        }
        if (details.relativePath.pathString.startsWith('assets/chickens/textures/entity/') && details.name.endsWith('.png')) {
            segments = details.relativePath.segments.clone()
            segments[segments.length - 1] = details.name.toLowerCase(Locale.ROOT)
            details.relativePath = new RelativePath(details.relativePath.isFile(), segments)
        }
    }
    filesMatching('assets/chickens/models/**/*.json') {
        filter { line ->
            line.replace('chickens:blocks/', 'chickens:block/').replace('chickens:items/', 'chickens:item/')
        }
    }
}

dependencies {
    // Hook into JEI so the modernised recipe categories can render in-game guidance.
    implementation "mezz.jei:jei-${project.minecraft_version}-neoforge:${project.jei_version}"
}

neoForge {
    version = project.neoforge_version
    addModdingDependenciesTo(sourceSets.main)
    runs {
        client { client() }
        server { server() }
        data {
        data()
        programArguments.addAll("--mod", "chickens")
    }
    }
    mods {
        modernchickens {
            sourceSet sourceSets.main
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
